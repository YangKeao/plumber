//
// Created by intellij-pest on 2019-04-16
// plumber
// Author: yangkeao
//

WHITESPACE = _{ " " }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

definition_prefix = _{ ext }
    ext = {"extern"}

binary_operation = _{ plus | mul | minus | div }
    plus = {"+"}
    mul = {"*"}
    minus = {"-"}
    div = {"/"}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

keywords = {"let" | "in"}

variable_name = @{ !keywords ~ ident }

const_value = @{ ASCII_DIGIT }

function_call = {
    variable_name ~ (monadic_expression | ("(" ~ binary_expression ~ ")"))+
}

monadic_expression = {
    function_call |
    variable_name |
    const_value   |
    ("(" ~ monadic_expression ~ ")")
}

binary_expression = {
    monadic_expression ~ (binary_operation ~ expression) |
    ("(" ~ binary_expression ~ ")")
}

expression = {
    binary_expression |
    monadic_expression
}

binding = {
    variable_name ~ "=" ~ expression
}

local_binding = { "let" ~ (binding ~ "\n"? ~ "and")* ~ binding ~ "\n"? ~ "in" }

fun_definition = { definition_prefix* ~ "let" ~ variable_name ~ variable_name* ~ "=" ~ "\n"? ~ local_binding? ~ "\n"? ~ expression }

statement = _{ fun_definition }

program = _{ SOI ~ "\n"* ~ (statement ~ "\n"+)* ~ statement? ~ EOI }
